#ASM [
#INCLUDE "startup.asm"
app_entry:
    inc rsp, 4
    pop a0 // stream_local
    pop a1 // arg0
    call Main
    jmp app_exit

app_name: .ds "Filer" .db 0
.global app_name
app_desc: .ds "File Manager" .db 0
.global app_desc
app_author: .ds "fox32 contributors (github.com/fox32-arch)" .db 0
.global app_author
app_version: .ds "0.0.0" .db 0
.global app_version
]

#DEFINE BLD_BITS 32
#DEFINE ARCHITECTURE "fox32"
#INCLUDE "sys/rtl.hjk"
#INCLUDE "sys/os.hjk"

#INCLUDE "window.hjk"
#INCLUDE "desktop.hjk"
#INCLUDE "filetype.hjk"
#INCLUDE "newdir.hjk"
#INCLUDE "getinfo.hjk"

text_stream : ^File = NULLPTR
PUBLIC windows : ^IconWindow[MAX_WINDOWS]

icons_file : File
PUBLIC icons_graphics : ^UBYTE = NULLPTR

filetype_file : File
PUBLIC filetype_data : ^UBYTE = NULLPTR

EXTERN link_icon : UBYTE[256]

EXTERN menu_bar : Menu

EXTERN new_dir_window_open : UBYTE
EXTERN get_info_window_open : UBYTE

EXTERN FN GetTick() : ULONG
#ASM [
GetTick:
.global GetTick
    in a3, 0x80000706
    ret
]

FN (RtlPrintCallbackF) RtljPrintCallback(
    IN byte : UBYTE,
    IN context : ^VOID,
)
    IF text_stream != 0 THEN
        buffer : UBYTE[1]
        buffer[0] = byte
        Write(text_stream, &buffer[0], 1)
    END
END
FN RtljLockStream(IN handle : ^VOID) : UWORD
    // dummy function to keep the compiler happy
END
FN RtljUnlockStream(
    IN handle : ^VOID,
    IN lockcontext : UWORD,
)
    // dummy function to keep the compiler happy
END

EXTERN FN app_exit()
FN Exit()
    w := 0
    WHILE w < MAX_WINDOWS DO
        IF windows[w] != NULLPTR THEN
            CloseWindow(w)
        END
        w += 1
    END
    IF icons_graphics != NULLPTR THEN
        FreeMemory(icons_graphics)
    END
    app_exit()
END

FN Error(IN error_string : ^UBYTE)
    RtlPrint(error_string)
    RtlPrint("\n")
    Exit()
END

FN Initialize(
    IN stream_local : ^File,
)
    text_stream = stream_local

    // load icons into memory if the resource file exists
    IF Open("/system/icons.res", GetBootDiskId(), &icons_file) THEN
        icons_file_size := GetSize(&icons_file)
        icons_graphics = AllocateMemory(icons_file_size)
        Read(&icons_file, icons_graphics, icons_file_size)
        linkIconPtr := GetResource(icons_graphics, "lnk", 256)
        IF linkIconPtr != NULLPTR THEN
            CopyMemoryBytes(linkIconPtr, &link_icon[0], 256)
            FreeMemory(linkIconPtr)
        END
    END

    // load file type associations into memory if the resource file exists
    IF Open("/system/filetype.res", GetBootDiskId(), &filetype_file) THEN
        filetype_file_size := GetSize(&filetype_file)
        filetype_data = AllocateMemory(filetype_file_size)
        Read(&filetype_file, filetype_data, filetype_file_size)
    END
END

FN HandleMouseClick(
    IN windowNumber : UWORD,
    IN x : UINT,
    IN y : UINT,
)
    IF y < 16 THEN
        // user clicked title bar
        IF x < 8 THEN
            // user clicked close button
            CloseWindow(windowNumber)
            LEAVE
        END

        // otherwise, start dragging the window
        StartDraggingWindow(&windows[windowNumber]^.Window)
        LEAVE
    END

    IF windows[windowNumber]^.WindowType == WINDOW_TYPE_DESKTOP THEN
        // desktop window should be refreshed when clicked
        old_num_icons := windows[windowNumber]^.NumIcons
        RefreshDesktopFiles(windowNumber)
        IF windows[windowNumber]^.NumIcons != old_num_icons THEN
            RenderDesktop(windowNumber)
        END
    END

    HandleWidgetClick(&windows[windowNumber]^.Window, x, y)
END

FN HandleButtonClick(
    IN windowNumber : UWORD,
    IN buttonId : UWORD,
)
    IF windows[windowNumber]^.ClickIcon == buttonId THEN
        // the last-clicked icon was this one
        // check if too much time has passed
        IF windows[windowNumber]^.ClickTimeMax < GetTick() THEN
            // too much time passed, reset the timer and return
            windows[windowNumber]^.ClickTimeMax = GetTick() + DOUBLE_CLICK_TICKS
            LEAVE
        END
        // otherwise, reset the time and icon and continue
        windows[windowNumber]^.ClickTimeMax = 0
        windows[windowNumber]^.ClickIcon = 0xFF
    ELSE
        // a new button was just clicked, set up the timer and return
        windows[windowNumber]^.ClickTimeMax = GetTick() + DOUBLE_CLICK_TICKS
        windows[windowNumber]^.ClickIcon = buttonId
        RenderWindow(windowNumber, FALSE)
        LEAVE
    END

    IF windows[windowNumber]^.Icon[buttonId].IsDisk THEN
        diskPath : UBYTE[4]
        RtlFormat(&diskPath[0], 4, "%d:/", windows[windowNumber]^.Icon[buttonId].DiskId)
        OpenWindow(&diskPath[0])
    ELSE
        // build up the new path string
        concatPath : UBYTE[256]
        nameLen := StringLength(&windows[windowNumber]^.Icon[buttonId].Name[0])
        AppendNameToPath(&windows[windowNumber]^.Path[0], &windows[windowNumber]^.Icon[buttonId].Name[0], &concatPath[0])
        OpenFileByType(windows[windowNumber], buttonId, &concatPath[0], windows[windowNumber]^.Disk)
    END
END

FN HandleMenuClick (
    IN windowNumber : UWORD,
    IN menu : UWORD,
    IN item : UWORD,
)
    IF menu == 0 AND item == 0 THEN
        // "New"
        OpenNewDirWindow(windowNumber, &windows[windowNumber]^.Path[0])
    ELSEIF menu == 0 AND item == 1 THEN
        // "Get Info"
        concatPath : UBYTE[256]
        IF windows[windowNumber]^.ClickIcon == 0xFF OR windows[windowNumber]^.ClickIcon >= windows[windowNumber]^.NumIcons THEN LEAVE END
        IF windows[windowNumber]^.Icon[windows[windowNumber]^.ClickIcon].IsDisk THEN LEAVE END // TODO: support getting disk info
        AppendNameToPath(&windows[windowNumber]^.Path[0], &windows[windowNumber]^.Icon[windows[windowNumber]^.ClickIcon].Name[0], &concatPath[0])
        OpenGetInfoWindow(windowNumber, &windows[windowNumber]^.Icon[windows[windowNumber]^.ClickIcon], &concatPath[0])
    END
END

FN Main(
    IN stream_local : ^File,
    IN arg0 : ^UBYTE,
)
    Initialize(stream_local)
    running := TRUE

    IF arg0 != NULLPTR AND CompareString(arg0, "desktop") THEN
        OpenDesktop()
    ELSEIF arg0 != NULLPTR AND arg0[1] == ':' THEN
        OpenWindow(arg0)
    ELSE
        RtlPrint("filer - fox32os file manager\nusage: filer <dir path>\n")
    END

    WHILE running == TRUE DO
        w := 0
        openWindows := 0
        WHILE w < MAX_WINDOWS DO
            IF windows[w] == NULLPTR THEN w += 1 CONTINUE END
            openWindows += 1
            e : Event
            GetNextWindowEvent(&windows[w]^.Window, &e)
            IF e.Type == EVENT_TYPE_MOUSE_CLICK THEN
                HandleMouseClick(w, e.Body.Pos.X, e.Body.Pos.Y)
            ELSEIF e.Type == EVENT_TYPE_BUTTON_CLICK THEN
                HandleButtonClick(w, e.Body.Parameters[0])
            ELSEIF e.Type == EVENT_TYPE_MENU_BAR_CLICK THEN
                MenuBarClickEvent(&menu_bar, e.Body.Pos.X)
            ELSEIF e.Type == EVENT_TYPE_MENU_UPDATE THEN
                MenuUpdateEvent(&menu_bar, e.Body.MenuChoice.Menu, e.Body.MenuChoice.Item)
            ELSEIF e.Type == EVENT_TYPE_MENU_CLICK THEN
                HandleMenuClick(w, e.Body.MenuChoice.Menu, e.Body.MenuChoice.Item)
            ELSEIF e.Type == EVENT_TYPE_MENU_ACK THEN
                CloseMenu(&menu_bar)
            END
            YieldTask()
            w += 1
        END
        IF new_dir_window_open THEN
            UpdateNewDirWindow()
        END
        IF get_info_window_open THEN
            UpdateGetInfoWindow()
        END
        IF openWindows == 0 THEN
            running = FALSE
        END
    END

    Exit()
END
